apply plugin: 'com.android.application'
apply plugin: 'io.objectbox'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        applicationId "cn.nicolite.huthelper"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'//, 'armeabi', 'x86'//, 'x86_64', "mips"
        }

        manifestPlaceholders = [
                XG_ACCESS_ID : "2100267021",
                XG_ACCESS_KEY: "AK8J5TS61K8D",
                MTA_APPKEY   : "ACIDT96D3M7R",
                MTA_CHANNEL  : "ddc"
        ]
    }

    dexOptions {
        jumboMode true
        javaMaxHeapSize "2g"
    }

    signingConfigs {
        //从signing.properties文件中拉取签名信息
        def keystoreStream = new FileInputStream(rootProject.file('signing.properties'))
        def keyProperties = new Properties()
        keyProperties.load(keystoreStream)

        config {
            keyAlias keyProperties.getProperty('KEY_ALIAS')
            keyPassword keyProperties.getProperty('KEY_PWD')
            storeFile file(keyProperties.getProperty('KEYSTORE_FILE'))
            storePassword keyProperties.getProperty('KEYSTORE_PWD')
        }

        release {
            keyAlias keyProperties['KEY_ALIAS']
            keyPassword keyProperties['KEY_PWD']
            storeFile file(keyProperties['KEYSTORE_FILE'])
            storePassword keyProperties['KEYSTORE_PWD']
        }

        debug {
            keyAlias keyProperties['KEY_ALIAS']
            keyPassword keyProperties['KEY_PWD']
            storeFile file(keyProperties['KEYSTORE_FILE'])
            storePassword keyProperties['KEYSTORE_PWD']
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            buildConfigField("boolean", "LOG_DEBUG", "false");
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            buildConfigField("boolean", "LOG_DEBUG", "true");
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    packagingOptions {
        //rxjava1和自己的rxjava2冲突
        exclude 'META-INF/rxjava.properties'
    }

    productFlavors {
        //酷安
        kuan {
            manifestPlaceholders = [
                    MTA_CHANNEL  : "酷安"
            ]
        }
        //小米
        xiaomi {
            manifestPlaceholders = [
                    MTA_CHANNEL  : "小米应用商店"
            ]
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:26.0.0-alpha1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:design:26.0.0-alpha1'
    compile 'com.android.support:cardview-v7:26.0.0-alpha1'
    compile 'com.android.support:recyclerview-v7:26.0.0-alpha1'
    compile 'com.android.support:support-annotations:26.0.0-alpha1'
    testCompile 'junit:junit:4.12'
    //多dex配置,解决dex超过64k
    compile 'com.android.support:multidex:1.0.1'
    //retrofit okhttp rxjava
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.9.0'

    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    //生命周期管理
    compile 'com.trello.rxlifecycle2:rxlifecycle:2.1.0'
    // If you want to bind to Android-specific lifecycles
    compile 'com.trello.rxlifecycle2:rxlifecycle-android:2.1.0'
    // If you want pre-written Activities and Fragments you can subclass as providers
    compile 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    //Glide--图片加载
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.5.0'
    //图片滤镜处理
    compile 'jp.wasabeef:glide-transformations:2.0.2'
    // If you want to use the GPU Filters（使用GPU）
    compile 'jp.co.cyberagent.android.gpuimage:gpuimage-library:1.4.1'
    //Luban---图片压缩
    compile 'top.zibin:Luban:1.1.3'
    //PhotoView----图片放大缩小
    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    //图片选择
    compile 'com.zhihu.android:matisse:0.4.3'
    //ButterKnife
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'
    compile 'com.jakewharton:butterknife:8.7.0'
    //Gson---json解析
    compile 'com.google.code.gson:gson:2.8.1'
    //动画低版本支持
    compile 'com.nineoldandroids:library:2.4.0'
    //权限管理
    compile 'pub.devrel:easypermissions:1.0.1'
    // bugly升级sdk
    compile 'com.tencent.bugly:crashreport_upgrade:1.3.1'
    //滑动退出Activity
    compile 'com.r0adkll:slidableactivity:2.0.5'
    //网页抓取
    compile 'org.jsoup:jsoup:1.10.3'
    //图表
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    //LRecyclerView
    compile 'com.github.jdsjlzx:LRecyclerView:1.4.3'
    //小红点
    compile 'q.rorbin:badgeview:1.1.2'
    //信鸽推送
    compile 'com.tencent.xinge:xinge:3.1.8-alpha'
    //wup包 如果和其他腾讯系的sdk 发生wup冲突，这个依赖可不添加
    compile 'com.tencent.wup:wup:1.0.0.E-alpha'
    //mid包
    compile 'com.tencent.mid:mid:3.74-alpha'
    //MTA的jar包
    compile 'com.qq.mta:mta:3.1.4-alpha'
    //融云kit
    compile project(':IMKit')
    //数据库
    ext.objectboxVersion = '1.1.0'
    compile "io.objectbox:objectbox-android:$objectboxVersion"
    annotationProcessor "io.objectbox:objectbox-processor:$objectboxVersion"
}
