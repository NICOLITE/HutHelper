import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.greenrobot.greendao'

static def gitVersionCode() {
    def cmd = 'git rev-list HEAD --count'
    def cmdExecute = cmd.execute().text.trim()
    def gitVersionCode = "190514${cmdExecute}"
    return gitVersionCode.toInteger()
}

static def gitVersionTag() {
    def cmd = 'git describe --tags --always --dirty="-dev"'
    def gitVersionTag = cmd.execute().text.trim()
    gitVersionTag = gitVersionTag.replaceFirst("-", ".").replace("\"", "")
    return gitVersionTag
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "cn.nicolite.huthelper"
        minSdkVersion 19
        targetSdkVersion 27
        versionName gitVersionTag()
        versionCode gitVersionCode()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a'//, 'x86', 'armeabi'//, 'x86_64', "mips"
        }
    }

    signingConfigs {
        //从signing.properties文件中拉取签名信息
        def keystoreStream = new FileInputStream(rootProject.file('signing.properties'))
        def keyProperties = new Properties()
        keyProperties.load(keystoreStream)

        config {
            keyAlias keyProperties.getProperty('KEY_ALIAS')
            keyPassword keyProperties.getProperty('KEY_PWD')
            storeFile file(keyProperties.getProperty('KEYSTORE_FILE'))
            storePassword keyProperties.getProperty('KEYSTORE_PWD')
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }

        debug {
            minifyEnabled false
            zipAlignEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    greendao {
        schemaVersion 3
        daoPackage "cn.nicolite.huthelper.db.dao"
        targetGenDir "src/main/java"
        generateTests false
        targetGenDirTests "src/androidTest/java"
    }

    applicationVariants.all {
        variant ->
            variant.outputs.all {
                SimpleDateFormat format = new SimpleDateFormat("yyyy-MM-dd", Locale.CHINA)
                String releaseTime = format.format(new Date())
                outputFileName = "huthelper_${variant.flavorName}_${variant.versionCode}_${variant.versionName}_${releaseTime}.apk"
            }
    }

    productFlavors {
        //release
        releases {
            flavorDimensions "release"
        }
        //beta
        beta {
            flavorDimensions "beta"
        }
    }

    /**
    * 添加kotlin源代码目录
    * */
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:support-annotations:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:multidex:1.0.3'

    //retrofit okhttp rxjava
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.5.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //生命周期管理
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android:2.2.2'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.2'

    //Glide--图片加载
    implementation 'com.github.bumptech.glide:glide:3.8.0'

    //自定义实现加载进度监听 copy source code

    //    implementation 'com.github.bumptech.glide:okhttp3-integration:1.5.0'

    //图片滤镜处理
    implementation 'jp.wasabeef:glide-transformations:2.0.2'

    //Luban---图片压缩
    implementation 'top.zibin:Luban:1.1.3'

    //PhotoView----图片放大缩小
    implementation 'com.github.chrisbanes:PhotoView:2.1.4'

    //图片选择
    implementation 'com.zhihu.android:matisse:0.4.3'

    //ButterKnife
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.jakewharton:butterknife:8.8.1'

    //Gson---json解析
    implementation 'com.google.code.gson:gson:2.8.5'

    //动画低版本支持
    implementation 'com.nineoldandroids:library:2.4.0'

    //权限管理
    implementation 'com.yanzhenjie:permission:1.1.2'

    //网页抓取
    implementation 'org.jsoup:jsoup:1.11.3'

    //LRecyclerView
    implementation 'com.github.jdsjlzx:LRecyclerView:1.5.4.4'

    //数据库
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.1.0'

    //内存泄露检测
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'

    //滑动返回
    implementation project(':swipebackhelper')

    //mvp base
    implementation 'com.github.nicolite:archer:1.3.3'
}
